/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'io.spinnaker.project' version "$spinnakerGradleVersion" apply false
  id "org.jetbrains.kotlin.jvm" version "$kotlinVersion" apply false
  id "org.jetbrains.kotlin.plugin.allopen" version "$kotlinVersion" apply false
}

allprojects {
  apply plugin: 'io.spinnaker.project'
  repositories {mavenCentral()}
  tasks.withType(Copy).all {
    duplicatesStrategy 'exclude'
  }
}

subprojects {
  group = "io.spinnaker.fiat"

  if ([korkVersion].find { it.endsWith("-SNAPSHOT") }) {
    repositories {
      mavenLocal()
      mavenCentral()
      maven{
        url  "NEXUS_URL"
        credentials {
                    username = "NEXUS_USERNAME"
                    password = "NEXUS_PASSWORD"
                     }
      }
    }
  }

  if (name != "fiat-bom") {
    apply plugin: 'java-library'
    apply plugin: 'groovy'

    test {
      testLogging {
        exceptionFormat = 'full'
        afterSuite { desc, result ->
          if (!desc.parent) {
            println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            println "Report file: ${reports.html.entryPoint}"
          }
        }
      }
      minHeapSize = "512m"
      maxHeapSize = "512m"
      useJUnitPlatform()
    }
    tasks.withType(Javadoc) {
      failOnError = false
    }

    tasks.withType(JavaExec) {
      if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=7103'
      }
    }

    dependencies {
      implementation enforcedPlatform("io.spinnaker.kork:kork-bom:$korkVersion")
      implementation "io.spinnaker.kork:kork-swagger"
      compileOnly "org.projectlombok:lombok"
      annotationProcessor platform("io.spinnaker.kork:kork-bom:$korkVersion")
      annotationProcessor "org.projectlombok:lombok"
      annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
      testAnnotationProcessor platform("io.spinnaker.kork:kork-bom:$korkVersion")
      testAnnotationProcessor "org.projectlombok:lombok"

      testImplementation "org.springframework.boot:spring-boot-starter-test"
      testImplementation "org.spockframework:spock-core"
      testImplementation "org.spockframework:spock-spring"
      testImplementation "org.springframework:spring-test"
      testImplementation "org.hamcrest:hamcrest-core"
      testRuntimeOnly "cglib:cglib-nodep:3.3.0"
      testRuntimeOnly "org.junit.vintage:junit-vintage-engine"
      testRuntimeOnly "org.objenesis:objenesis"
    }
  }
}

defaultTasks ':fiat-web:run'
